let
  // define function
  #"GET Activity" = (accessToken as text, optional startDate as text, optional endDate as text, optional continuationToken as text, optional loop as number, optional data as list) =>
    let
	// Source is the Power BI REST API base URL
    Source = Json.Document(Web.Contents("https://api.powerbi.com/v1.0/myorg/",
    // add conditional relative path to the source
	[
	// Define starting loop parameters
    RelativePath=if loop = 0
    then "admin/activityevents?startDateTime=" & startDate & "&endDateTime=" & endDate
    // If it's not the starting loop then add the continuation token to the relative path
	else "admin/activityevents?continuationToken='" & continuationToken & "'",
    // add headers to the web call
	Headers=[Authorization="Bearer " & accessToken]
    ]
    )),
    // define 'token' as the continuation token
	token = Source[continuationToken],
    // define the data in the current loop
	currentData = Source[activityEventEntities],
    // define how to combine current data with previously retrieved data
	appendedData = List.Combine({data, currentData}),
    // increment the loop by 1 to keep count
	loopNum = loop + 1,
    // combine the results by iterating until continuation token is null
	output =
    if token is null or loopNum > 100
    then appendedData
    else @#"GET Activity"(accessToken, "", "", token, loopNum, appendedData)
in
    output
in
  #"GET Activity"